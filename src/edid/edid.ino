// Deliver a customized EDID to the host computer.
// Uses an Attiny1404, with standard I2C wiring.

// EDID data block
uint8_t edid[128] = {
  0x00,0xff,0xff,0xff,0xff,0xff,0xff,0x00,0x0d,0xf0,0x01,0x00,0x01,0x00,0x00,0x00,
  0x0a,0x22,0x01,0x03,0xa2,0x28,0x1e,0x78,0x26,0xb7,0xf5,0xa8,0x54,0x35,0xad,0x25,
  0x10,0x50,0x54,0x00,0x00,0x00,0x01,0x01,0x01,0x01,0x01,0x01,0x01,0x01,0x01,0x01,
  0x01,0x01,0x01,0x01,0x01,0x01,0x88,0x0a,0xa0,0x20,0x51,0x20,0x18,0x10,0x18,0x80,
  0x33,0x00,0x90,0x2c,0x11,0x00,0x00,0x18,0x00,0x00,0x00,0xfc,0x00,0x48,0x44,0x4d,
  0x49,0x32,0x53,0x43,0x41,0x52,0x54,0x0a,0x20,0x20,0x00,0x00,0x00,0x10,0x00,0x00,
  0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x10,
  0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x0c
};

uint8_t registeraddress = 0;
bool didreceiveaddress = false;

void setup() {
  // set up LUT0 to combine HSYNC and VSYNC
  CCL.LUT0CTRLB   = 0x55;       // take inputs 0 and 1 from dedicated pin
  CCL.LUT0CTRLC   = 0x05;       // take input 2 from dedicated pin
  CCL.TRUTH0      = B11000011;  // truth table for LUT0
  CCL.LUT0CTRLA   = B00001001;  // enable LUT0 and output pin
  CCL.CTRLA = B00000001;        // enable CCL

  // set up the TWI (I2C) slave
  pinMode(6, INPUT_PULLUP);
  pinMode(7, INPUT_PULLUP);
  TWI0.CTRLA       = B00011100;  // most conservative behaviour
  TWI0.SCTRLB      = B00000000;  // standard ACK behaviour
  TWI0.SADDR       = B10100000;  // client address $50, no response to "general call address"
  TWI0.SCTRLA      = B11100011;  // enable TWI client in smart mode and allow interrupts
}

void loop() 
{
}

ISR(TWI0_TWIS_vect)
{
  switch (TWI0.SSTATUS & B11000011)
  {
  case B01000001:     // address interrupt for writing
    didreceiveaddress = false;
    break;
  case B10000000:     // data interrupt when writing
  case B10000001:
    if (! didreceiveaddress) 
    {
      registeraddress = TWI0.SDATA;   // already does ACK
      didreceiveaddress = true;
      return;
    }
    break;
  case B10000010:     // data interrupt when reading
  case B10000011:
    TWI0.SDATA = edid[registeraddress & 0x7F];
    registeraddress++;
    break;
  }
  TWI0.SCTRLB = 0x03;  // acknowledge every case
}
